<html>
<HEAD>
<HTA:APPLICATION ID="oHTA"
     APPLICATIONNAME="myApp"
     BORDER="thin"
     BORDERSTYLE="normal"
     CAPTION="yes"
     ICON="icon.ico"
	 SCROLL="no"
     MAXIMIZEBUTTON="no"
     MINIMIZEBUTTON="yes"
     SHOWINTASKBAR="yes"
     SINGLEINSTANCE="yes"
     SYSMENU="yes"
     VERSION="-.-"
	 CONTEXTMENU="no"
     WINDOWSTATE="normal"/>  
<SCRIPT Language="VBScript">
Sub Window_onLoad
On Error Resume Next
    window.resizeTo 460 , 290
	Set objFSO = CreateObject("Scripting.FileSystemObject")
	Set f = objFSO.OpenTextFile("V")
	document.title= document.title & f.ReadLine
	
	For Each objFolder In objFSO.GetFolder("AAA").SubFolders
		Set opt = document.createElement("option")
		myselect.options.add(opt)
		opt.text = objFolder.Name
		opt.value = objFolder.Name
	Next
End Sub

Sub SelectFile
    pngFile.value = ""
    strStartPath = ""
    strFilter = "Image (*.png;*.apng)|*.png;*.apng|All Files (*.*)|*.*|"
    strCaption = "Select a File"
    pngFile.value = Dlg.openfiledlg(CStr(strStartPath), , CStr(strFilter), CStr(strCaption))
	prev.src=pngFile.value
End Sub

Sub Process
On Error Resume Next
    'Get file path from INPUT
    Dim file: file = pngFile.Value
    Dim selects: selects = myselect.Value

	'hide and show buttons
	logs.style.display= "table"
	comenz.style.display= "none"
	brow.style.display= "none"
	pngFile.style.display= "none"
	myselect.style.display= "none"
	
    Set objShell = CreateObject("Wscript.Shell")
	objShell.Run "cmd /c rmdir ips /s/q",0,true	
    objShell.Run "cmd /c mkdir ips",0,true

	Set objFSO = CreateObject("Scripting.FileSystemObject")

	If(selects = "all") Then 
		For Each objFolder In objFSO.GetFolder("AAA").SubFolders
			logs.Value= "Creando ips para "+objFolder.Name
			objShell.Run "dist\pito\pito.exe AAA\"+objFolder.Name+" "+file,0,true
		Next
	else
		logs.Value= "Creando ips para "+selects
		objShell.Run "dist\pito\pito.exe AAA\"+selects+" "+ file,0,true
	End If

	dim folder: set folder = objFSO.getFolder("ips")
	if folder.files.Count = 0 then
		objShell.PopUp "Ha Habido un error convirtiendo:"& vbCrLf& file &" ",8,"Fallido",16
	else
		objShell.Run "cmd /c mkdir ips\atmosphere\exefs_patches\logo",0,true
		objShell.Run "cmd /c mkdir ips\sxos\exefs_patches\logo",0,true
		objShell.Run "cmd /c xcopy /Y/I ips\*.ips ips\atmosphere\exefs_patches\logo\",0,true	
		objShell.Run "cmd /c move /y ips\*.ips ips\sxos\exefs_patches\logo\",0,true	
		logs.Value= "Terminado"
		objShell.PopUp "Termine con  "& file &", revisa IPS",8,"Terminado",64
		objShell.Run "cmd /c explorer ips",0,true
	End If
	
	'hide and show buttons
	logs.style.display= "none"
	comenz.style.display= "table"
	brow.style.display= "table"
	pngFile.style.display= "table"
	myselect.style.display= "table"
End Sub
</SCRIPT>
</HEAD>
<body style="background-color: #63b6eb; color: black;text-align: center;" id="body">
<title>Logo para Switch by D3fau4 & Kronos2308 -.- v</title>
<p style="background-color: #a5cbf0">
PNG To IPS Logo Creator 1.0.0-12.x.x<br>
"PNG" "308x350" "RGBA"<br>
</p>
FirmW <select name="myselect"><option value="all">Todos</option></select><br>
<input type="text" name="pngFile" Value="temp.png" size="40">
<input style="background-color: #0994ed; border: 2px solid white;	color: white;" type="button" name="brow" onClick="SelectFile" value="Browse..."><br><br>
<input type="text" name="logs" style="display: none" size="30">
<img name="prev" src="temp.png" alt="none" style="width:50px; height:55px;"><br>
<input style="background-color: #056aab; border: 2px solid white; padding: 10px 15px;	color: white;	font-size: 14px;" type="button" name="comenz" onClick="Process" value="Convertir">
<OBJECT id=Dlg classid="CLSID:3050F4E1-98B5-11CF-BB82-00AA00BDCE0B" width=0 height=0>
</body>
<!--
0100000000000023
010000000000002D
-->
</html>